#Maintainer Mike Harris <mike.a.harris@outlook.com>
pkgname=plasma5-applets-kde-arch-update-notifier-git
pkgver=7.2.1.r10.g5b454f4
pkgrel=1
pkgdesc="A KDE plasmoid for your system tray notifying you of available updates with AUR Support"
url="https://github.com/hikmet517/kde-arch-update-plasmoid"
arch=('x86_64')
license=('GPL')
depends=("konsole" "pacman-contrib" "plasma-workspace" "qt5-xmlpatterns")
optdepends=("checkupdates-aur" "yakuake")
makedepends=("make" "cmake" "git" "extra-cmake-modules" "plasma-framework")
conflicts=()
replaces=()
backup=()
install=
source=("git+https://github.com/hikmet517/kde-arch-update-plasmoid")
md5sums=("SKIP")

pkgver() {
  cd "$srcdir/kde-arch-update-plasmoid"
  # cutting off 'foo-' prefix that presents in the git tag
  printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g')"
}

build() {
    cd kde-arch-update-plasmoid
    cmake -B build -S . \
          -DBUILD_TESTING=OFF \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DKDE_INSTALL_LIBDIR=lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON

    cmake --build build
}

package(){
    cd ${srcdir}/kde-arch-update-plasmoid
    DESTDIR="$pkgdir" cmake --install build
}
