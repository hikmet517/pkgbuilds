# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cinemagoer
pkgname=python-cinemagoer-git
pkgver=2023.05.01.r40.g193ec42
pkgrel=1
pkgdesc="Cinemagoer (previously known as IMDbPY) is a Python package for retrieving and managing the data of the IMDb movie database about movies and people"
url="https://cinemagoer.github.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("git+https://github.com/cinemagoer/cinemagoer.git")
b2sums=('SKIP')

pkgver() {
  cd ${_name}
  git describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_name}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_name}"
  python setup.py install --optimize=1 --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
