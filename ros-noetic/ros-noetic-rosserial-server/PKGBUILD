pkgdesc="ROS - A more performance- and stability-oriented server alternative
implemented in C++ to rosserial_python."
url='https://wiki.ros.org/rosserial_server'

pkgname='ros-noetic-rosserial-server'
pkgver='0.9.2'
arch=('i686' 'x86_64' 'aarch64' 'armv7h' 'armv6h')
pkgrel=1
license=('BSD')

ros_makedepends=(
    ros-noetic-catkin
    ros-noetic-rosserial-msgs
    ros-noetic-std-msgs
    ros-noetic-roscpp
    ros-noetic-topic-tools
)

makedepends=(
    'cmake'
    'ros-build-tools'
    ${ros_makedepends[@]}
)

ros_depends=(
    ros-noetic-rosserial-msgs
    ros-noetic-rosserial-python
    ros-noetic-std-msgs
    ros-noetic-roscpp
    ros-noetic-topic-tools
)

depends=(
    ${ros_depends[@]}
)

_dir="rosserial-${pkgver}/rosserial_server"
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/ros-drivers/rosserial/archive/${pkgver}.tar.gz"
        "boost.patch")
sha256sums=('10544be94241499aa3b019aea6d7cb40546ea484749e909cee31a928c7d40465'
            '1efddf20ea6e1ba716dd960a64e43b8c28ae1139dd159d0b4dc7af57d6ed7244')

prepare() {
    cd ${srcdir}/${_dir}
    patch --strip=2 --input="${srcdir}/boost.patch"
}

build() {
    # Use ROS environment variables.
    source /usr/share/ros-build-tools/clear-ros-env.sh
    [ -f /opt/ros/noetic/setup.bash ] && source /opt/ros/noetic/setup.bash

    # Create the build directory.
    [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
    cd ${srcdir}/build

    # Build the project.
    cmake ${srcdir}/${_dir} \
        -DCATKIN_ENABLE_TESTING=OFF \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/noetic \
        -DPYTHON_EXECUTABLE=/usr/bin/python \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}/" install
}
